{ python3Packages
, stdenv
, fetchFromGitHub
, fetchgit
, gnumake
, wrapQtAppsHook
, xvfb_run
, autobahn
, humanize
, hyperlink
, magic-wormhole
, pynacl
, pyqt5
, pyqtchart
, pyyaml
, qt5reactor
, treq
, twisted
, txtorcon
, zxcvbn
, atomicwrites
, distro
, tahoe-lafs-env

, tox
, pytest
, pytestcov
, pytesttwisted
, pytestqt
}:
let
  privatestorage = ../.;
  gridsync = fetchFromGitHub {
    owner = "gridsync";
    repo = "gridsync";
    # A recent-ish revision of 238.zkap-auth-support plus a couple packaging
    # fixes stacked on top.  Hopefully this can move to some master revision
    # before much longer.
    rev = "246a8d2609e79a52075d77d65a9e526a3aea267c";
    sha256 = "1xydz3i78d5w8c5p82aa4safrvn0byy96hwbx86hawy2fnl0jslq";
  };
in python3Packages.buildPythonApplication rec {
  version = "v0.4.2.dev";
  pname = "privatestorageclient";
  name = "${pname}-${version}";
  srcs = [
    privatestorage
    gridsync
  ];

  sourceRoot = "./";

  nativeBuildInputs = [
    wrapQtAppsHook
  ];

  buildInputs = pythonPath;

  propagatedBuildInputs = [ tahoe-lafs-env ];

  # pythonPath is deprecated but setting it is the only way to get run-time
  # dependencies injected by the wrapper generated by buildPythonApplication!
  pythonPath = [
    atomicwrites
    autobahn
    distro
    humanize
    hyperlink
    magic-wormhole
    pynacl
    pyqt5
    pyqtchart
    pyyaml
    qt5reactor
    treq
    twisted
    twisted.extras.tls
    txtorcon
    zxcvbn
  ];

  checkInputs = [
    tox
    pytest
    pytestcov
    pytesttwisted
    pytestqt
    xvfb_run
  ];

  postPatch = ''
    mv source PrivateStorageDesktop/privatestorage
    pushd PrivateStorageDesktop
    sed -i -e 's,git clone https://github.com/gridsync/gridsync privatestorage,,' Makefile
    sed -i -e 's,/bin/bash,${stdenv.shell},' Makefile
    popd
  '';

  preBuild = ''
    pushd PrivateStorageDesktop
    make source
    pushd privatestorage
  '';

  # Wrap it ourselves because non-ELF executables are ignored by the automatic
  # wrapping logic.  Do this in postFixup so that the Nixpkgs Python support
  # can do the Python wrapping.  wrapProgram replaces the Python script with a
  # shell script which won't be recognized by wrapPythonPrograms, I suppose.
  postFixup = ''
    wrapProgram "$out/bin/gridsync" "''${qtWrapperArgs[@]}"
  '';

  doCheck = false;

  # The tests depend on a newer version of Twisted than is currently packaged.
  # Hopefully we can enable these when Nixpkgs updates Twisted or we package a
  # newer version ourselves.
  checkPhase = ''
    ${xvfb_run}/bin/xvfb-run -a pytest tests
  '';
}
